buildscript {
    ext.versions = [
            'kotlin'       : '1.3.50',
            'build_tools'  : '29.0.2',
            'compile_sdk'  : 29,
            'target_sdk'   : 29,
            'min_sdk'      : 16,
            'androidx'     : [
                    'appcompat'        : '1.1.0',
                    'browser'          : '1.2.0-alpha07',
                    'constraint_layout': '2.0.0-beta2',
                    'core_ktx'         : '1.2.0-alpha04',
                    'dynamicanimation' : '1.1.0-alpha02'
            ],
            'play_services': '17.0.0',
            'ktlint'       : '0.33.0',
            'rxbinding'    : '3.0.0',
            'rxjava'       : '2.2.12',
            'rxkotlin'     : '2.4.0',
            'rxlint'       : '1.7.4',
            'leakcanary'   : '2.0-beta-3'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.5.0'
        classpath 'com.github.triplet.gradle:play-publisher:2.3.0'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.9.5'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$versions.kotlin"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.24.2'
    id 'com.github.ben-manes.versions' version '0.24.0'
    id 'io.gitlab.arturbosch.detekt' version '1.0.1'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
            allWarningsAsErrors = true
        }
    }
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
        }
    }
    apply plugin: 'io.gitlab.arturbosch.detekt'
    detekt {
        toolVersion = '1.0.1'
        filters = '.*/resources/.*,.*/build/.*'
    }
}
