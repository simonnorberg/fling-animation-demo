buildscript {
    ext.versions = [
            'kotlin'       : '1.3.70',
            'compile_sdk'  : 29,
            'target_sdk'   : 29,
            'min_sdk'      : 16,
            'androidx'     : [
                    'appcompat'        : '1.1.0',
                    'browser'          : '1.2.0',
                    'constraint_layout': '2.0.0-beta4',
                    'core_ktx'         : '1.2.0',
                    'dynamicanimation' : '1.1.0-alpha03'
            ],
            'play_services': '17.0.0',
            'ktlint'       : '0.36.0',
            'rxbinding'    : '3.1.0',
            'rxjava'       : '2.2.19',
            'rxkotlin'     : '2.4.0',
            'rxlint'       : '1.7.5',
            'leakcanary'   : '2.2'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.6.1'
        classpath 'com.github.triplet.gradle:play-publisher:2.7.2'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.10.2'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$versions.kotlin"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.27.2'
    id 'com.github.ben-manes.versions' version '0.28.0'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.8'
            allWarningsAsErrors = true
        }
    }
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint).userData(['disabled_rules': 'import-ordering'])
        }
    }
}
