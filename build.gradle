buildscript {
    ext.versions = [
            'kotlin'       : '1.3.21',
            'compile_sdk'  : 28,
            'target_sdk'   : 28,
            'min_sdk'      : 16,
            'androidx'     : [
                    'appcompat'        : '1.1.0-alpha03',
                    'browser'          : '1.0.0',
                    'constraint_layout': '2.0.0-alpha3',
                    'core_ktx'         : '1.1.0-alpha05',
                    'dynamicanimation' : '1.0.0'
            ],
            'play_services': '16.0.2',
            'ktlint'       : '0.31.0',
            'rxbinding'    : '3.0.0-alpha2',
            'rxjava'       : '2.2.8',
            'rxkotlin'     : '2.3.0',
            'rxlint'       : '1.7.3'
    ]
    repositories {
        google()
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:3.4.0-rc02'
        classpath 'com.github.triplet.gradle:play-publisher:2.1.1'
        classpath 'com.google.android.gms:oss-licenses-plugin:0.9.4'
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$versions.kotlin"
        classpath "org.jetbrains.kotlin:kotlin-android-extensions:$versions.kotlin"
    }
}

plugins {
    id 'com.diffplug.gradle.spotless' version '3.21.1'
    id 'com.github.ben-manes.versions' version '0.21.0'
    id 'io.gitlab.arturbosch.detekt' version '1.0.0-RC14'
}

allprojects {
    repositories {
        google()
        jcenter()
    }
}

subprojects {
    tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
        kotlinOptions {
            jvmTarget = '1.6'
            allWarningsAsErrors = true
        }
    }
    apply plugin: 'com.diffplug.gradle.spotless'
    spotless {
        kotlin {
            target '**/*.kt'
            ktlint(versions.ktlint)
        }
    }
    apply plugin: 'io.gitlab.arturbosch.detekt'
    detekt {
        toolVersion = '1.0.0-RC14'
        filters = '.*/resources/.*,.*/build/.*'
    }
}
